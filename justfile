container := "scapy_web_ui"

# ğŸ‘‰ Enables commands to be selected interactively.
default:
    @just --choose

# ğŸ‘· Runs a CI build.
ci: install lint spellcheck

# ğŸ‘Ÿ Runs the app!
run:
    sudo uv run main.py

# ğŸ§ª Runs unit tests interactively.
[group("test")]
test_interactive:
    pytest --headed

# ğŸš¨ Linting all files.
[group("lint")]
lint: lint_nodejs lint_python

# ğŸš¨ Linting Node.js files.
[group("lint")]
lint_nodejs:
    pnpm lint

# ğŸš¨ Linting Python files.
[group("lint")]
lint_python:
    uv run ruff check

# ğŸš¨ Fixing any lint errors.
[group("lint")]
lint_fix: lint_fix_nodejs lint_fix_python

# ğŸš¨ Fixing any Node.js lint errors.
[group("lint")]
lint_fix_nodejs:
    pnpm lint:fix

# ğŸš¨ Fixing any Python lint errors.
[group("lint")]
lint_fix_python:
    uv run ruff format

# ğŸ“ Spellchecks all files.
spellcheck:
    pnpm spellcheck

# ğŸ§¹ Removes screenshots from previous runs.
clean:
    rm screenshots/*.png

# ğŸ’¾ Installs all dependencies.
install: install_python install_nodejs

# ğŸ§ Installs system packages on Fedora.
[group("setup")]
setup_fedora:
    dnf install --assumeyes \
        "graphviz*" \
        chromedriver \
        gnuplot \
        ImageMagick \
        nmap \
        p0f \
        python-devel \
        python-matplotlib \
        python2-pyx.x86_64 \
        tcpdump \
        texlive

# ğŸ§ Installs system packages on Ubuntu.
[group("setup")]
setup_ubuntu:
    sudo apt-get install -y \
        apt-transport-https \
        apt-utils \
        build-essential \
        chromium-chromedriver \
        libgtk-4-dev \
        nmap \
        p0f \
        software-properties-common \
        tcpdump

# ğŸ’¾ Installs Python dependencies.
[group("install")]
install_python:
    uv sync
    uv run playwright install-deps

# ğŸ’¾ Installs Node.js dependencies.
[group("install")]
install_nodejs:
    pnpm install

# ğŸ³ Builds and runs a Docker container for portable testing.
[group("docker")]
docker: docker_build docker_run

# ğŸ³ Builds a Docker container.
[group("docker")]
docker_build:
    docker build -f Dockerfile -t {{ container }} .

# ğŸ³ Runs the test Docker container API.
[group("docker")]
docker_run:
    docker run -d --name scapy-web-ui -p 5000:5000 {{ container }}

# ğŸ³ Runs the test Docker container interactively.
[group("docker")]
docker_run_it:
    docker run -it {{ container }}
